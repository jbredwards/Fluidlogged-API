buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven-publish'

version = 'v2.2.6-mc1.12.2'
group = 'git.jbredwards.fluidlogged_api'
archivesBaseName = 'Fluidlogged-API'
sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'
    mappings = 'stable_39'
    makeObfSourceJar = false
}

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/public/' }
    maven { url 'https://cursemaven.com' }
}

dependencies {
    //optional mod compat
    deobfProvided 'io.github.opencubicchunks:cubicchunks-api:1.12.2-0.0-SNAPSHOT'
    deobfProvided 'curse.maven:better-foliage-228529:3393955'
    deobfProvided 'curse.maven:dynamic-lights-227874:2563244'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
}

jar {
    manifest {
        attributes([
                'FMLCorePlugin': 'git.jbredwards.fluidlogged_api.mod.asm.ASMHandler',
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLAT': 'fluidlogged_api_at.cfg'
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact sourcesJar
        }
    }
}

//for using asm with intellij
runClient { jvmArgs '-Dfml.coreMods.load=git.jbredwards.fluidlogged_api.mod.asm.ASMHandler' }
runServer { jvmArgs '-Dfml.coreMods.load=git.jbredwards.fluidlogged_api.mod.asm.ASMHandler' }
